// Top-level build file where you can add configuration options common to all sub-projects/modules.

////////////////////////////////////////////////////////////////////////////////////////////////////
// Ensure your ~/.gradle/gradle.properties file or environment defines the following properties:
// MAVEN_USER = your user name on the above Artifactory.
// MAVEN_PASS = your password on the above Artifactory.
////////////////////////////////////////////////////////////////////////////////////////////////////

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

ext {
    versionName = "1.0.3"
    versionCode = 3
    isSnapshot = false
}

ext.pomInfo = {
    packaging 'aar'
    url 'https://github.com/cfmobile/push-android'

    scm {
        url 'git@github.com:cfmobile/push-android.git'
        connection 'scm:git:git@github.com:cfmobile/push-android.git'
        developerConnection 'scm:git:git@github.com:cfmobile/push-android.git'
    }

    licenses {
        license {
            name 'Pivotal Software License'
            url 'http://www.pivotal.io/products/software-license-agreement'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'rszumlakowski'
            name 'Robert Szumlakowski'
            email 'rszumlakowski@pivotal.io'
        }
    }
}

allprojects {
    apply plugin: 'maven'

    group = "io.pivotal.android"

    if (isSnapshot) {
        version =  versionName + "-SNAPSHOT"
    } else {
        version =  versionName
    }

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'android-library'

    android {
        compileSdkVersion 21
        buildToolsVersion "21.1.2"

        defaultConfig {
            versionCode versionCode
            versionName versionName
            minSdkVersion 14
            targetSdkVersion 21
        }
    }

    configurations {
        archives {
            extendsFrom configurations.default
        }
    }

    uploadArchives {
        configuration = configurations.archives
        repositories.mavenDeployer {

            // Publishes to local repository
            // repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath

            // Publishes to remote repository
            repository(url: 'https://maven.xtremelabs.com/artifactory/libs-release-local') {
                authentication(userName: MAVEN_USER, password: MAVEN_PASS)
            }
            snapshotRepository(url: 'https://maven.xtremelabs.com/artifactory/libs-snapshot-local') {
                authentication(userName: MAVEN_USER, password: MAVEN_PASS)
            }
        }
    }
}
